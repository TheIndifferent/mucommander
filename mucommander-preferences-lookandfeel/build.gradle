plugins {
    id 'biz.aQute.bnd.builder'
}

repositories.mavenCentral()

dependencies {
    api project(":mucommander-core")

    implementation 'org.osgi:osgi.core:7.0.0'
    implementation 'org.slf4j:slf4j-api:1.7.26'

    comprise 'com.formdev:flatlaf:1.3'
    comprise 'com.formdev:flatlaf-intellij-themes:1.3'
}

jar {
    // flatlaf contains some classes that we do not want to include in OSGI bundle,
    // so we will package it as part of our bundle:
    from(configurations.comprise.collect { it.isDirectory() ? it : zipTree(it) }) {
        eachFile { f ->
            // those files will be moved from META-INF/versions/9/ :
            if (f.relativePath.pathString.startsWith('com/formdev/flatlaf/util/LoggingFacadeImpl') || f.relativePath.pathString.startsWith('com/formdev/flatlaf/util/MultiResolutionImageSupport')) {
                f.relativePath = new RelativePath(true, f.relativePath.parent, f.relativePath.lastName + ".jdk8")
            }
            // not copying module-info from dependencies:
            if (f.relativePath.pathString.contains('module-info.class')) {
                f.relativePath = new RelativePath(true, f.relativePath.parent, f.relativePath.lastName + ".deleted")
            }
            // move JDK9 classes as main ones:
            if (f.relativePath.pathString.startsWith('META-INF/versions/9/')) {
                f.relativePath = new RelativePath(true, f.relativePath.segments.drop(1).drop(1).drop(1))
            }
            // not using MigLayout and it causes OSGI failures, excluding it:
            if (f.relativePath.pathString.contains('MigLayout')) {
                f.relativePath = new RelativePath(true, f.relativePath.parent, f.relativePath.lastName + ".deleted")
            }
        }
    }

    // clearing classpath so bnd will not copy all the packages blindly after we filtered them out:
    classpath = []

    bnd(
            'Bundle-Name': 'mucommander-preferences-lookandfeel',
            'Bundle-Vendor': 'muCommander',
            'Bundle-Description': 'Look and Feel preferences',
            'Bundle-DocURL': 'https://www.mucommander.com',
            'Bundle-Activator': 'com.mucommander.preferences.lookandfeel.Activator',
            'Export-Package': 'com.mucommander.preferences.lookandfeel',
            'Import-Package': '*',
            'Specification-Version': project.version + '-' + project.ext.release,
            'Implementation-Title': "muCommander",
            'Implementation-Version': revision.substring(0, 7),
            'Build-Date': new Date().format('yyyyMMdd'),
            'Build-Url': "https://www.mucommander.com/version/nightly.xml")
}
